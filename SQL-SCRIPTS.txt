-- Criação do Banco de Dados
CREATE DATABASE INTERFACECHALLENGE;
GO

-- Selecionar o Banco de Dados
USE INTERFACECHALLENGE;
GO

-- Criação da Tabela TIPO
CREATE TABLE TIPO (
	ID BIGINT IDENTITY(1,1) NOT NULL,
	CODIGO VARCHAR(3) NOT NULL,
	DESCRICAO VARCHAR(60) NOT NULL,
	PRIMARY KEY (ID)
);
GO

-- Inserção de Dados na Tabela TIPO
INSERT INTO TIPO (CODIGO, DESCRICAO) VALUES
	('03', 'RECUSA POR FALTA DE PEDIDO DE COMPRA'),
	('04', 'RECUSA POR PEDIDO DE COMPRA CANCELADO'),
	('500', 'INCIDENTE'),
	('91', 'ENTREGA AGENDADA'),
	('01', 'ENTREGA REALIZADA COM SUCESSO');
GO

-- Criação da Tabela TRANSPORTADOR
CREATE TABLE TRANSPORTADOR (
	ID BIGINT IDENTITY(1,1) NOT NULL,
	CNPJ VARCHAR(14) NOT NULL,
	DESCRICAO VARCHAR(60) NOT NULL,
	PRIMARY KEY (ID)
);
GO

-- Inserção de Dados na Tabela TRANSPORTADOR
INSERT INTO TRANSPORTADOR (CNPJ, DESCRICAO) VALUES
	('03279710000271', 'TOMASI'),
	('05435749000185', 'ARGIUS'),
	('11040609000291', 'HDLOG');
GO

-- Criação da Tabela OCORRENCIA
CREATE TABLE OCORRENCIA (
	ID BIGINT IDENTITY(1,1) NOT NULL,
	ID_TIPO BIGINT NOT NULL,
	ID_TRANSPORTADOR BIGINT NOT NULL,
	OCORREU_EM DATETIME NOT NULL,
	SOLUCAO_EM DATETIME,
	PRIMARY KEY (ID)
);
GO

-- Inserção de Dados na Tabela OCORRENCIA
DECLARE @CONTADOR INT = 1;

WHILE (@CONTADOR < 1000)
BEGIN
	INSERT INTO OCORRENCIA (ID_TIPO, ID_TRANSPORTADOR, OCORREU_EM, SOLUCAO_EM)
		SELECT 
			A.ID AS ID_TIPO,
			B.ID AS ID_TRANSPORTADOR,
			(GETDATE() - C.ALEATORIO) AS OCORREU_EM,
			(CASE WHEN @CONTADOR % 2 = 0 THEN (GETDATE() - C.ALEATORIO) + 2 ELSE NULL END) AS SOLUCAO_EM
		FROM
			(SELECT TOP 1 * FROM TIPO ORDER BY NEWID()) AS A
		OUTER APPLY (SELECT TOP 1 ID FROM TRANSPORTADOR ORDER BY NEWID()) AS B
		OUTER APPLY (SELECT ABS(CHECKSUM(NEWID())) % 14 AS ALEATORIO) AS C
		ORDER BY NEWID();
	
	-- Incrementa o Contador
	SET @CONTADOR = @CONTADOR + 1;
END;
GO
